# Configuration Cloud-Init pour installer Nginx et forcer TLS 1.2
locals {
  custom_data_script = <<CUSTOM_DATA
#!/bin/bash
apt-get update
apt-get install -y nginx openssl

# Création d'un certificat auto-signé
mkdir -p /etc/nginx/ssl
openssl req -x509 -nodes -days 365 -newkey rsa:2048 \
    -keyout /etc/nginx/ssl/nginx.key \
    -out /etc/nginx/ssl/nginx.crt \
    -subj "/C=FR/ST=PACA/L=Nice/O=Security/CN=backend.local"

# Configuration Nginx minimaliste avec TLS 1.2 uniquement
cat > /etc/nginx/sites-available/default <<EOF
server {
    listen 80;
    listen 443 ssl;
    ssl_certificate /etc/nginx/ssl/nginx.crt;
    ssl_certificate_key /etc/nginx/ssl/nginx.key;
    
    # FORCING TLS 1.2 ONLY
    ssl_protocols TLSv1.2;
    ssl_ciphers HIGH:!aNULL:!MD5;

    location / {
        add_header Content-Type text/plain;
        return 200 'Bravo ! Vous avez atteint le Backend TLS 1.2 via FortiGate.';
    }
}
EOF

systemctl restart nginx
CUSTOM_DATA
}

resource "azurerm_network_interface" "backend_nic" {
  name                = "backend-nic"
  location            = var.location
  resource_group_name = azurerm_resource_group.myterraformgroup.name

  ip_configuration {
    name                          = "internal"
    subnet_id                     = azurerm_subnet.privatesubnet.id
    private_ip_address_allocation = "Dynamic"
  }
}

resource "azurerm_linux_virtual_machine" "backend_vm" {
  name                = "Backend-Web-Server"
  resource_group_name = azurerm_resource_group.myterraformgroup.name
  location            = var.location
  size                = var.backend_size
  admin_username      = var.adminusername
  network_interface_ids = [
    azurerm_network_interface.backend_nic.id,
  ]

  admin_password                  = var.adminpassword
  disable_password_authentication = false

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "0001-com-ubuntu-server-jammy"
    sku       = "22_04-lts"
    version   = "latest"
  }

  custom_data = base64encode(local.custom_data_script)

  tags = {
    environment = "Backend TLS 1.2"
  }
}